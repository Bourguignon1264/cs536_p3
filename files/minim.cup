/**********************************************************************
 Java CUP specification for a parser for minim programs
 **********************************************************************/

import java_cup.runtime.*;
import java.util.*;

/* The code below redefines method syntax_error to give better error messages
 * than just "Syntax error"
 */
parser code {:

public void syntax_error(Symbol currToken) {
    if (currToken.value == null) {
        ErrMsg.fatal(0,0, "Syntax error at end of file");
    }
    else {
        ErrMsg.fatal(((TokenVal)currToken.value).lineNum,
                     ((TokenVal)currToken.value).charNum,
                     "Syntax error");
    }
    System.exit(-1);
}
:};


/* Terminals (tokens returned by the scanner) */
terminal                INT;
terminal                BOOL;
terminal                VOID;
terminal                TRUE;
terminal                FALSE;
terminal                STRUCT;
terminal                INPUT;
terminal                DISP;
terminal                IF;
terminal                ELSE;
terminal                WHILE;
terminal                RETURN;
terminal IdTokenVal     ID;
terminal IntLitTokenVal INTLITERAL;
terminal StrLitTokenVal STRINGLITERAL;
terminal                LCURLY;
terminal                RCURLY;
terminal                LPAREN;
terminal                RPAREN;
terminal                SEMICOLON;
terminal                COMMA;
terminal                DOT;
terminal                WRITE;
terminal                READ;
terminal                PLUSPLUS;
terminal                MINUSMINUS;
terminal                PLUS;
terminal                MINUS;
terminal                TIMES;
terminal                DIVIDE;
terminal                NOT;
terminal                AND;
terminal                OR;
terminal                EQUALS;
terminal                NOTEQUALS;
terminal                LESS;
terminal                GREATER;
terminal                LESSEQ;
terminal                GREATEREQ;
terminal                ASSIGN;


/* Nonterminals
 *
 * NOTE: You will need to add more nonterminals to this list as you
 *       add productions to the grammar below.
 */
non terminal ProgramNode      program;
non terminal LinkedList       declList;
non terminal DeclNode         decl;
non terminal VarDeclNode      varDecl;
non terminal TypeNode         type;
non terminal IdNode           id;
non terminal LinkedList       varDeclList;
non terminal FnDeclNode       fnDecl;
non terminal StructDeclNode   structDecl;
non terminal LinkedList/*StructBodyNode*/ structBody;
non terminal FormalsListNode/*FormalsNode*/ formals;
non terminal LinkedList       formalsList;
non terminal FormalDeclNode   formalDecl;
non terminal FnBodyNode       fnBody;
non terminal LinkedList       stmtList;
non terminal StmtNode         stmt;
non terminal AssignExpNode    assignExp;
non terminal ExpNode          exp;
non terminal ExpNode/*TermNode*/ term;
non terminal CallExpNode/*FncallNode*/ fncall;
non terminal LinkedList       actualList;
non terminal ExpNode/*LocNode*/ loc;
 
 
/* NOTE: Add precedence and associativity declarations here */
precedence right ASSIGN;
precedence left OR;
precedence left AND;
precedence nonassoc LESS, GREATER, LESSEQ, GREATEREQ, EQUALS, NOTEQUALS;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence left NOT;
/* TODO: maybe include MINUS in highest precedence for unary operator? */
/* TODO: maybe include DOT in highest precedence? */


start with program;


/* Grammar with actions
 *
 * NOTE: add more grammar rules below
 */
program         ::= declList: d
                {: RESULT = new ProgramNode(new DeclListNode(d));
                :}
                ;

declList        ::= declList:dl decl:d
                {: dl.addLast(d);
                   RESULT = dl;
                :}
                | /* epsilon */
                {: RESULT = new LinkedList<DeclNode>();
                :}
                ;

decl            ::= varDecl:v
                {: RESULT = v;
                :}
                | fnDecl:f
                {: RESULT = f;
                :}
                | structDecl:s
                {: RESULT = s;
                :}

                ;

varDecl         ::= type:t id:i SEMICOLON
                {: RESULT = new VarDeclNode(t, i, VarDeclNode.NOT_STRUCT);
                :}
                | STRUCT id:j id:i SEMICOLON
                {: RESULT = new VarDeclNode(new StructNode(j), i, 0);
                :}
                ;

type            ::= INT
                {: RESULT = new IntNode();
                :}
                | BOOL
                {: RESULT = new BoolNode();
                :}
                | VOID
                {: RESULT = new VoidNode();
                :}
                ;

id              ::= ID:i
                {: RESULT = new IdNode(i.lineNum, i.charNum, i.idVal);
                :}
                ;

varDeclList     ::= varDeclList:l varDecl:d
                {: l.addLast(d);
                   RESULT = l;
                :}
                | /* epsilon */
                {: RESULT = new LinkedList<DeclNode>();
                :}
                ;

fnDecl          ::= type:t id:i formals:f fnBody:b
                {: RESULT = new FnDeclNode(t, i, f, b);
                :}
                ;

structDecl      ::= STRUCT id:i LCURLY structBody:b RCURLY SEMICOLON
                {: RESULT = new StructDeclNode(i, new DeclListNode(b));
                :}
                ;

structBody      ::= structBody:b varDecl:v
                {: b.addLast(v);
                   RESULT = b;
                :}
                | varDecl:v
                {: RESULT = new LinkedList<>();
                   RESULT.addLast(v);
                :}
                ;

formals         ::= LPAREN RPAREN
                {: RESULT = new FormalsListNode(new LinkedList<>());
                :}
                | LPAREN formalsList:l RPAREN
                {: RESULT = new FormalsListNode(l);
                :}
                ;

formalsList     ::= formalDecl:d
                {: LinkedList<FormalDeclNode> l = new LinkedList<>();
                   l.addLast(d); // TODO: maybe addFirst()??
                   RESULT = l;
                :}
                | formalDecl:d COMMA formalsList:l
                {: l.addLast(d); // TODO: maybe addFirst()??
                   RESULT = l;
                :}
                ;

formalDecl      ::= type:t id:i // No struct parameters
                {: RESULT = new FormalDeclNode(t,i);
                :}
                ;

fnBody          ::= LCURLY varDeclList:v stmtList:s RCURLY
                {: RESULT = new FnBodyNode(new DeclListNode(v), new StmtListNode(s));
                :}
                ;

stmtList        ::=stmtList:l stmt:s
                {: sl.addLast(s);
                   RESULT = sl;
                :}
                | /* epsilon */
                {: RESULT = new LinkedList<StmtNode>();
                :}
                ;

stmt            ::= assignExp:a SEMICOLON
                {: RESULT = new AssignStmtNode(a);
                :}
                | loc:l PLUSPLUS SEMICOLON
                {: RESULT = new PostIncStmtNode(l);
                :}
                | loc:l MINUSMINUS SEMICOLON
                {: RESULT = new PostDecStmtNode(l);
                :}
                | INPUT READ loc:l SEMICOLON
                {: RESULT = new ReadStmtNode(l);
                :}
                | DISP WRITE exp:e SEMICOLON
                {: RESULT = new WriteStmtNode(e);
                :}
                | IF exp:e LCURLY varDeclList:v stmtList:s RCURLY
                {: RESULT = new IfStmtNode(e, new DeclListNode(v), new StmtListNode(s));
                :}
                | IF exp:e LCURLY varDeclList:v stmtList:s RCURLY ELSE LCURLY varDeclList:w stmtList:t RCURLY
                {: RESULT = new IfElseStmtNode(e, new DeclListNode(v), new StmtListNode(s), new DeclListNode(w), new StmtListNode(t));
                :}
                | WHILE exp:e LCURLY varDeclList:v stmtList:s RCURLY
                {: RESULT = new WhileStmtNode(e, new DeclListNode(v), new StmtListNode(s));
                :}
                | RETURN exp:e SEMICOLON
                {: RESULT = new ReturnStmtNode(e);
                :}
                | RETURN SEMICOLON
                {: RESULT = new ReturnStmtNode(NULL);
                :}
                | fncall:f SEMICOLON
                {: RESULT = new CallStmtNode(f);
                :}
                ;

assignExp       ::= loc:l ASSIGN exp:e
                {: RESULT = new AssignExpNode(l, e);
                :}
                ;

exp             ::= assignExp:a
                {: RESULT = a;
                :}
                | exp:e1 PLUS exp:e2
                {: RESULT = new PlusNode(e1,e2);
                :}
                | exp:e1 MINUS exp:e2
                {: RESULT = new MinusNode(e1,e2);
                :}
                | exp:e1 TIMES exp:e2
                {: RESULT = new TimesNode(e1,e2);
                :}
                | exp:e1 DIVIDE exp:e2
                {: RESULT = new DivideNode(e1,e2);
                :}
                | NOT exp:e1
                {: RESULT = new NotNode(e1);
                :}
                | exp:e1 AND exp:e2
                {: RESULT = new AndNode(e1,e2);
                :}
                | exp:e1 OR exp:e2
                {: RESULT = new OrNode(e1,e2);
                :}
                | exp:e1 EQUALS exp:e2
                {: RESULT = new EqualsNode(e1,e2);
                :}
                | exp:e1 NOTEQUALS exp:e2
                {: RESULT = new NotEqualsNode(e1,e2);
                :}
                | exp:e1 LESS exp:e2
                {: RESULT = new LessNode(e1,e2);
                :}
                | exp:e1 GREATER exp:e2
                {: RESULT = new GreaterNode(e1,e2);
                :}
                | exp:e1 LESSEQ exp:e2
                {: RESULT = new LessEqNode(e1,e2);
                :}
                | exp:e1 GREATEREQ exp:e2
                {: RESULT = new GreaterEqNode(e1,e2);
                :}
                | MINUS exp:e1
                {: RESULT = new UnaryMinusNode(e1);
                :}
                | term:t
                {: RESULT = t;
                :}
                ;

term            ::= loc:l
                {: RESULT = l;
                :}
                | INTLITERAL:i
                {: RESULT = new IntLitNode(i.lineNum, i.charNum, i.intVal);
                :}
                | STRINGLITERAL:s
                {: RESULT = new StringLitNode(s.lineNum, s.charNum, s.strVal);
                :}
                | TRUE:t
                {: RESULT = new TrueNode(t.lineNum, t.charNum);
                :}
                | FALSE:f
                {: RESULT = new FalseNode(f.lineNum, f.charNum);
                :}
                | LPAREN exp:e RPAREN
                {: RESULT = e;
                :}
                | fncall:f
                {: RESULT = f;
                :}
                ;

fncall          ::= id:i LPAREN RPAREN
                {: RESULT = new CallExpNode(i, null);
                :}
                | id:i LPAREN actuallist:a RPAREN
                {: RESULT = new CallExpNode(i, new ExpListNode(a));
                :}
                ;

actualList      ::= exp:e
                {:
                LinkedList<ExpNode> list = new LinkedList<>();
                list.addLast(e);
                RESULT = list;
                :}
                | actualList:a COMMA exp:e
                {:
                a.addLast(e);
                RESULT = a;
                :}
                ;

loc             ::= id:i
                {: RESULT = i;
                :}
                | loc:l DOT id:i
                {: RESULT = new DotAccessExpNode(l,i);
                :}
                ;
				